apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "com.ibotta.gradle.aop"

android {
    defaultConfig {
        applicationId "me.jdvp.androidaspectexample"
        minSdkVersion 23
        targetSdkVersion 31
        compileSdkVersion 31
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    /*
    Required when using above Kotlin 1.5, otherwise Kotlin lambdas will be compiled using
    LambdaMetafactory.metafactory() which means that aspects cannot be matched since the
    generated lambdas won't have the expected method names. You can see my write-up of the
    issue here:
    https://github.com/Ibotta/gradle-aspectj-pipeline-plugin/issues/8
     */
    kotlinOptions {
        freeCompilerArgs = ["-Xsam-conversions=class"]
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    lintOptions {
        checkAllWarnings true
        warningsAsErrors true

        informational "NewerVersionAvailable",
                "GradleDependency",
                "ConvertToWebp"
    }
}

dependencies {
    //android sdk items
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:1.3.1"

    //AspectJ
    implementation "org.aspectj:aspectjrt:1.9.7"
}
